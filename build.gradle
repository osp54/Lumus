import fr.xpdustry.toxopid.spec.ModPlatform

plugins {
    id("java")
    id("fr.xpdustry.toxopid") version "3.0.0"
}

version '0.8'

targetCompatibility = 8
sourceCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
}

ext {
    // The build number that this mod is made for
    mindustryVersion = 'v143'
    jabelVersion = "93fde537c7"
    sdkRoot = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")
}

toxopid {
    compileVersion.set("v143")
    runtimeVersion.set("v143")
    platforms.add(ModPlatform.HEADLESS)
}

// Java 8 backwards compatibility flag
allprojects {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs.addAll(['--release', '8'])
        }
    }
}

dependencies {
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
    compileOnly "com.github.Anuken.Mindustry:server:$mindustryVersion"

    implementation 'party.iroiro.luajava:luajava:3.4.0'
    implementation 'party.iroiro.luajava:luajit:3.4.0'
    runtimeOnly 'party.iroiro.luajava:luajit-platform:3.4.0:natives-desktop'

    annotationProcessor "com.github.Anuken:jabel:$jabelVersion"
}

//force arc version
configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.github.Anuken.Arc') {
            details.useVersion "$mindustryVersion"
        }
    }
}


jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from(rootDir) {
        include "plugin.hjson"
    }
}

tasks.register("getProjectVersion") {
    doLast { println(project.version.toString()) }
}

tasks.runMindustryServer {
    mods.setFrom(tasks.jar)
}